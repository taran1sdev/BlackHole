#version 460 core
layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout(rgba16f, binding = 0) writeonly uniform image3D diskVolume;

uniform float rMin;
uniform float rMax;
uniform float zMin;
uniform float zMax;

uniform float uTime;

const float PI = 3.14159265359;

float hash(vec3 p) {
    p = fract(p * 0.3183099 + vec3(0.1, 0.2, 0.3));
    p += dot(p, p.yzx + 19.19);
    return fract(p.x * p.y * p.z * 157.0);
}

float noise_3(vec3 p) {
    // 3-octave cheap fractal noise
    float n  = hash(p);
    n += 0.5  * hash(p * 2.1);
    n += 0.25 * hash(p * 4.3);
    n /= 1.75;
    return n; 
}

// ---------------------------------------------------------------------
// MAIN
// ---------------------------------------------------------------------
void main() {
    ivec3 gid  = ivec3(gl_GlobalInvocationID);
    ivec3 size = imageSize(diskVolume);
    if (any(greaterThanEqual(gid, size))) return;

    vec3 uvw = (vec3(gid) + 0.5) / vec3(size);

    float theta = uvw.x * 2.0 * PI;
    float r     = mix(rMin, rMax, uvw.y);
    float z     = mix(zMin, zMax, uvw.z);

    float H = 0.6;//8 + 0.015 * r;
    float vert = exp(-0.5 * (z * z) / (H * H));

    float radial = pow(r, -1.4);
    
    float innerBright = exp(-0.5 * pow((r - rMin * 1.3) / 2.2, 2.0));
    radial *= (1.0 + 2.5 * innerBright);

    float rin = max(rMin * 1.1, 2.5);
    radial *= smoothstep(rin, rin + 3.0, r);

    radial *= (1.0 - smoothstep(rMax * 0.6, rMax, r));

    const float rISCO = 6.0;  
    float torus = exp(-0.5 * pow((r - rISCO) / 1.4, 2.0));
    float torusBoost = 1.0 + 1.8 * torus;

    float spiral = 0.5 + 0.5 * sin(5.0 * theta - 0.18 * r);
    
    float n = noise_3(vec3(theta * 1.2, r * 0.3, z * 1.4));
    float turbulence = mix(0.4, 1.8, n);

    float density = radial * vert * torusBoost;
    density *= mix(0.7, 1.3, spiral);
    density *= turbulence;

    density = max(density, 0.0);
    density *= 7;  
    
    float shear = sin(theta * 20.0 + r * 3.0 + uTime * 2.0);
    density *= mix(0.7, 1.6, shear * 0.5 + 0.5);

    float T = pow(r / rin, -2.2);
    T += 0.25 * (turbulence - 0.5);
    T *= (1.0 + 0.4 * torus);

    T = clamp(T, 0.0, 1.0);

    imageStore(diskVolume, gid, vec4(density, T, turbulence, 1.0));
}

