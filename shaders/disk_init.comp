#version 460 core
layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout(rgba16f, binding = 0) writeonly uniform image3D diskVolume;

uniform float rMin;
uniform float rMax;
uniform float zMin;
uniform float zMax;
uniform float densityScale;

const float PI = 3.14159265359;

float hash(vec3 p) {
    p = fract(p * 0.3183099 + vec3(0.1, 0.2, 0.3));
    p += dot(p, p.yzx + 19.19);
    return fract(p.x * p.y * p.z * 57.0);
}

void main() {
    ivec3 gid = ivec3(gl_GlobalInvocationID);
    ivec3 size = imageSize(diskVolume);
    if (any(greaterThanEqual(gid, size))) return;
    
    vec3 uvw = (vec3(gid) + 0.5) / vec3(size);

    float theta = uvw.x * 2.0 * PI;
    float r = mix(rMin, rMax, uvw.y);
    float z = mix(zMin, zMax, uvw.z);
    
    float H = 0.6;
    float vert = exp(-0.5 * (z * z) / (H * H));

    float rin = max(rMin * 1.1, 2.5);
    float rout = rMax;
    float x = clamp((r - rin) / max((rout - rin), 1e-3), 0.0, 1.0);

    float radial = pow(r, -3.0) * smoothstep(0.0, 0.1, x) * (1.0 - smoothstep(0.8, 1.0, x));

    float Tin = 1.0;
    float Tr = Tin * pow(r / rin, -0.75) * (1.0 - sqrt(rin / max(r, rin + 1e-3)));
    Tr = clamp(Tr, 0.0, 1.0);

    float density = radial * vert;

    // noise / spiral
    float noise_1 = hash(vec3(theta * 3.0, r * 0.5, z * 0.5));
    float noise_2 = hash(vec3(theta * 7.0, r * 1.5, z * 1.5));
    float noise = 0.6 * noise_1 + 0.4 * noise_2;

    float spiral = 0.5 + 0.5 * sin(6.0 * theta - 0.25 * r);
    density *= mix(0.7, 1.3, noise * spiral);

    density = max(density, 0.0);

    density *= 200.0f;

    imageStore(diskVolume, gid, vec4(density, Tr, noise, 1.0));
}
